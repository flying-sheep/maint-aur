# Maintainer:  Kyle Meyer <kyle@kyleam.com>
# https://github.com/kyleam/maint-aur/tree/master/snakemake

pkgname=snakemake
pkgver=3.1.1
pkgrel=1
pkgdesc='Python-based language and execution environment for GNU Make-like workflows'
arch=('any')
url='https://bitbucket.org/johanneskoester/snakemake/wiki/Home'
depends=('python')
makedepends=('python-setuptools')
optdepends=(
  'graphviz: For DAG visualization'
  'python-docutils: For report generation'
)
license=('MIT')
source=("https://bitbucket.org/johanneskoester/snakemake/get/v$pkgver.tar.gz")
md5sums=('6e6ea0d30c240b2abac767fe317ea1d1')

_srcdir='johanneskoester-snakemake-498db518770b'

build() {
  cd "$_srcdir"
  python setup.py build
}

package() {
  cd "$_srcdir"
  python setup.py install --skip-build -O1 --root="$pkgdir"
}
